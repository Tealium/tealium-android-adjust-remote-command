plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

group = 'com.tealium.remotecommands'
version = tealium_adjust_version

android {
    compileSdkVersion 32
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField 'String', 'TEALIUM_ADJUST_VERSION', "\"$tealium_adjust_version\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.7.0'

    api 'com.tealium:remotecommands:1.0.1'
    api 'com.adjust.sdk:adjust-android:[4.30,5.0)'
    implementation 'com.android.installreferrer:installreferrer:2.2'

    testImplementation 'junit:junit:4.+'
    testImplementation 'io.mockk:mockk:1.12.0'
    testImplementation "org.robolectric:robolectric:4.6.1"
}

afterEvaluate {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.release
                groupId 'com.tealium.remotecommands'
                artifactId 'adjust'
            }
        }

        repositories {
            maven {
                url "s3://maven.tealiumiq.com/android/releases/"
                credentials(AwsCredentials) {
                    accessKey AWS_ACCESS_KEY
                    secretKey AWS_SECRET_KEY
                }
            }
        }
    }
}